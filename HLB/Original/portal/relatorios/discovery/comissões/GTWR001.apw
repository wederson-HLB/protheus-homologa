#include "apwebex.ch"
#include "totvs.ch"
#include "tbiconn.ch"
#Include "Topconn.ch"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GTWR001 ºAutor  ³Eduardo C. Romanini  º  Data ³  25/10/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Relatorio de Faturamento da Discovery                       º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Portal Grant Thornton                                      º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
*---------------------*
User Function GTWR001()
*---------------------*
Local cHtml	:= "" 
Local cEmp    := Substr(HttpSession->cEmpresa,1,2)
Local cLoja   := Substr(HttpSession->cEmpresa,3,2)

WEB EXTENDED INIT cHtml

If cEmp <> "49"
	cHtml := "<html><body><h1>Essa empresa não possui acesso a esse relatorio</h1></body></html>" 
Else
	If Select("SX2") == 0
		U_WFPrepEnv()
	EndIf
	
	cHtml := ExecInPage("GTWR001")
	
EndIf

WEB EXTENDED END
	 
Return cHtml

/*
Função  : WR001Gera()
Objetivo: Gerar o relatório e exibi-lo na tela
Autor   : Eduardo C. Romanin
Data    : 06/11/2012 10:00
*/
*-----------------------*
User Function WR001Gera()
*-----------------------*
Local cHtml    := ""
Local cDataDe  := HttpPost->cDataDe 
Local cDataAte := HttpPost->cDataAte
Local cEmp     := Substr(HttpSession->cEmpresa,1,2)
Local cLoja    := Substr(HttpSession->cEmpresa,3,2)
Local cBanco   := ""
Local cIp      := ""
Local cDest    := ""

Local nCon := 0
Local nHdl := 0
Local nBytesSalvo := 0

Local dDataDe 
Local dDataAte

Local aCon := {}

WEB EXTENDED INIT cHtml

If Select("SX2") == 0
	U_WFPrepEnv()
EndIf

If HttpGet->cDataDe != Nil .And. HttpGet->cDataAte != Nil
	cDataDe := UnEscape(HttpGet->cDataDe)
	cDataAte := UnEscape(HttpGet->cDataAte)
EndIf

If !Empty(cDataDe) .and. !Empty(cDataAte)

	//Transforma os parametros recebidos em data
	dDataDe  := CtoD(cDataDe)
	dDataAte := CtoD(cDataAte)
	
	//Tranforma os parametros recebidos em string
	cDataDe  := DtoS(dDataDe)
	cDataAte := DtoS(dDataAte)

	//Retorna o ambiente da empresa logada
	aCon  := U_WFRetBanco(cEmp,cLoja)
	cBanco:= aCon[1]
	cIp   := aCon[2]
	
	//Realiza a conexão com o banco de dados da empresa
	//MSM - 09/06/2015 - Alterado a porta do top para o do novo ambiente
	nCon := TCLink(cBanco,cIp,7891)


	//Executa a query para gerar o relatório
	BeginSql Alias 'SQL'
		SELECT D2_DOC AS 'Documento' 
		      ,A1_COD AS 'CodCli'
		      ,A1_NOME AS 'NomeCli'
		      ,C5_P_AGC AS 'CodAgencia'
		      ,C5_P_NMAGC AS 'NomeAgencia'
		      ,C5_P_PI AS 'PI'
		      ,Upper(A3_NOME) AS 'Vendedor'
             ,C5_P_MAPA  as 'MapaRES'
		      ,B1_DESC AS 'Produto'
		      ,Convert(numeric(18,2),D2_TOTAL+D2_DESCON) AS 'VlBruto'
		      ,Convert(numeric(18,2),D2_DESCON) AS 'ComissAgencia'
		      ,Convert(numeric(18,2),D2_TOTAL) AS 'VlLiquido'
		      ,Convert(Varchar(10),Convert(SmallDateTime,D2_EMISSAO),103) AS 'Emissao'
		FROM SD2490 A
		    ,SB1490 B
		    ,SA1490 C
		    ,SC5490 E
		LEFT JOIN SA3490 SA3 ON C5_VEND1 = A3_COD AND SA3.%notDel%
		WHERE A.%notDel%   
		  AND B.%notDel%   
		  AND C.%notDel%   
		  AND E.%notDel%   
		  AND A.D2_PEDIDO = E.C5_NUM
		  AND A.D2_CLIENTE+A.D2_LOJA = C.A1_COD+C.A1_LOJA
		  AND A.D2_COD=B.B1_COD
		  AND A.D2_EMISSAO between %exp:cDataDe% and %exp:cDataAte% 
		ORDER BY D2_DOC
	EndSql
	
	//Imprime os valores na tela
	SQL->(DbGoTop())
	If SQL->(!BOF()) .and. SQL->(!EOF()) 
		HttpCtType('text/html; charset=iso-8859-1')
		
		// Se tiver dados modifica o tipo de retorno
		If HttpGet->cDataDe != Nil .And. HttpGet->cDataAte != Nil
			cNome := ''
			cDia := StrZero(Day(dDataBase), 2)
			cMes := StrZero(Month(dDataBase), 2)
			cAno := AllTrim(Str(Year(dDataBase)))
			
			cNome += AllTrim(HttpSession->cLogin) + '_'
			cNome += cAno + cMes + cDia + '_' + AllTrim(Str(Round(Seconds(),0)))
			
			HttpCtDisp('attachment; filename="' + cNome + '.xls"')
			HttpCtType('application/vnd.ms-excel')
		EndIf

		cHtml :=	"<table id='tb1'>" + CRLF
		cHtml +=	"	<thead>" + CRLF
		cHtml +=	"		<tr>" + CRLF
		cHtml +=	"			<th>"+ u_WFTraduzir("Documento") + "</th>" + CRLF
		cHtml +=	"			<th>"+ u_WFTraduzir("Cod Cliente") + "</th>" + CRLF
		cHtml +=	"			<th>"+ u_WFTraduzir("Nome Cliente") + "</th>" + CRLF
		cHtml +=	"			<th>"+ u_WFTraduzir("Cod Agencia") + "</th>" + CRLF
		cHtml +=	"			<th>"+ u_WFTraduzir("Nome Agencia") + "</th>" + CRLF
		cHtml +=	"			<th>"+ u_WFTraduzir("PI") + "</th>" + CRLF
		cHtml +=	"			<th>"+ u_WFTraduzir("Vendedor") + "</th>" + CRLF
		cHtml +=	"			<th>"+ u_WFTraduzir("Mapa RES") + "</th>" + CRLF
		cHtml +=	"			<th>"+ u_WFTraduzir("Produto") + "</th>" + CRLF
		cHtml +=	"			<th>"+ u_WFTraduzir("Valor Bruto") + "</th>" + CRLF
		cHtml +=	"			<th>"+ u_WFTraduzir("Comiss Agencia") + "</th>" + CRLF
		cHtml +=	"			<th>"+ u_WFTraduzir("Valor Liquido") + "</th>" + CRLF
		cHtml +=	"			<th>"+ u_WFTraduzir("Emissao") + "</th>" + CRLF
		cHtml +=	"		</tr>" + CRLF
		cHtml +=	"	</thead>" + CRLF
		cHtml +=	"	<tbody>" + CRLF
	
		While SQl->(!EOF())

			cHtml +=	"	<tr class='odd'>" + CRLF	
			cHtml +=	"		<td>"+AllTrim(SQL->Documento)+"</td>" + CRLF
			cHtml +=	"		<td>"+AllTrim(SQL->CodCli)+"</td>" + CRLF
			cHtml +=	"		<td>"+AllTrim(SQL->NomeCli)+"</td>" + CRLF
			cHtml +=	"		<td>"+AllTrim(SQL->CodAgencia)+"</td>" + CRLF
			cHtml +=	"		<td>"+AllTrim(SQL->NomeAgencia)+"</td>" + CRLF
			cHtml +=	"		<td>"+AllTrim(SQL->PI)+"</td>" + CRLF
			cHtml +=	"		<td>"+AllTrim(SQL->Vendedor)+"</td>" + CRLF
			cHtml +=	"		<td>"+AllTrim(SQL->MapaRES)+"</td>" + CRLF
			cHtml +=	"		<td>"+AllTrim(SQL->Produto)+"</td>" + CRLF
			cHtml +=	"		<td>"+StrTran(AllTrim(Str(SQL->VlBruto)), '.', ',')+"</td>" + CRLF
			cHtml +=	"		<td>"+StrTran(AllTrim(Str(SQL->ComissAgencia)), '.', ',')+"</td>" + CRLF
			cHtml +=	"		<td>"+StrTran(AllTrim(Str(SQL->VlLiquido)), '.', ',')+"</td>" + CRLF
			cHtml +=	"		<td>"+AllTrim(SQL->Emissao)+"</td>" + CRLF
			cHtml +=	"	</tr>" + CRLF
			
			//RRP - 13/10/2015 - Tratamento para não estourar o retorno da variável cHtml.
			If Len(cHtml) > 500000
				HttpSend(cHtml)
				cHtml:=""
			EndIf
			
			SQL->(DbSkip())
		EndDo
		
		cHtml +=	"	</tbody>" + CRLF
		cHtml +=	"</table>"
		
	Else
		cHtml := "<script type='text/javascript'>" + CRLF
		cHtml += "alert('Nao existe dados para o periodo informado.')" + CRLF
		cHtml += "</script>" + CRLF
	EndIf

	//Fecha o arquivo temporário
	SQL->(DbCloseArea())

	//Encerra a conexão
	TCunLink(nCon)
	
//Tratamento de erro
Else
	cHtml := "<script type='text/javascript'>" + CRLF
	cHtml += "alert('Os parametros nao foram informados corretamente')" + CRLF
	cHtml += "</script>" + CRLF

EndIf

WEB EXTENDED END

Return cHtml

