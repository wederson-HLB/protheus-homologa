#include "apwebex.ch"
#include "totvs.ch"
#include "tbiconn.ch"
#Include "Topconn.ch"



/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GTWR003 ºAutor  ³Daniel Fonseca Lira  º  Data ³  15/02/2013 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Relatorio vendsa da FSI                                     º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Portal Grant Thornton                                      º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/



*---------------------*
User Function GTWR003()
*---------------------*
	Local cHtml	:= "" 
	Local cEmp    := Substr(HttpSession->cEmpresa,1,2)
	Local cLoja   := Substr(HttpSession->cEmpresa,3,2)
	
	WEB EXTENDED INIT cHtml
	
	If cEmp <> "EF"
		cHtml := "<html><body><h1>Essa empresa não possui acesso a esse relatorio</h1></body></html>" 
	Else
		If Select("SX2") == 0
			U_WFPrepEnv()
		EndIf
		cHtml := ExecInPage("GTWR003")
	EndIf
	
	WEB EXTENDED END
Return cHtml



User Function WR003Html()
	Local dDataDe  := CToD(HttpPost->cDataDe) 
	Local dDataAte := CToD(HttpPost->cDataAte)
	Local cDataDe  := DtoS(dDataDe)
	Local cDataAte := DtoS(dDataAte)
Return WR003Gerar(.F., cDataDe, cDataAte)



User Function WR003Excel()
	Local cHtml := ''
	Local cNome := 'fsi_vendas_'
	Local cDia  := StrZero(Day(Date()), 2)
	Local cMes  := StrZero(Month(Date()), 2)
	Local cAno  := AllTrim(Str(Year(Date())))
	
	Local dDataDe  := CToD(HttpPost->cDataDe) 
	Local dDataAte := CToD(HttpPost->cDataAte)
	Local cDataDe  := DtoS(dDataDe)
	Local cDataAte := DtoS(dDataAte)
	
	cNome += cAno + cMes + cDia + '_' + AllTrim(Str(Round(Seconds(),0)))
			
	HttpCtDisp('attachment; filename="' + cNome + '.xls"')
	HttpCtType('application/vnd.ms-excel')
	
	cHtml += '<!DOCTYPE html>'
	cHtml += '<html>'
	cHtml += '	<head>'
	cHtml += '		<title>Relat&oacute;rio de vendas - FSI</title>'
	cHtml += '		<style>
	cHtml += '			thead th {'
	cHtml += '				border-bottom: thin solid black;'
	cHtml += '				background-color: #DDDDEE;'
	cHtml += '			}'
	cHtml += '			tbody td {'
	cHtml += '				border-bottom: thin dashed black;'
	cHtml += '				border-right: thin solid white;'
	cHtml += '			}'
	cHtml += '			.odd {'
	cHtml += '				background-color: #F5F5FA;'
	cHtml += '			}'
	cHtml += '		</style>'
	cHtml += '	</head>'
	cHtml += '<body>'
	cHtml += WR003Gerar(.T., cDataDe, cDataAte)
	cHtml += '</body>'
	cHtml += '</html>'
Return cHtml



Static Function WR003Gerar(lPlanilha, cDataDe, cDataAte)
	Local cEmp   := ''
	Local cFil   := ''
	Local cBanco := ''
	Local cIp    := ''
	Local nCon   := 0
	Local cHtml  := ''
	Local lImpar := .T.
	Local aCon   := {}
	
	cEmp := Substr(HttpSession->cEmpresa,1,2)
	cFil := Substr(HttpSession->cEmpresa,3,2)
	
	If Select("SX2") == 0
		U_WFPrepEnv()
	EndIf
	
	//Retorna o ambiente da empresa logada
	aCon  := U_WFRetBanco(cEmp,cLoja)
	cBanco:= aCon[1]
	cIp   := aCon[2]

	nCon := TCLink(cBanco,cIp,aCon[6])
	
	BeginSql Alias 'SQL'
		SELECT
			CONVERT(VARCHAR(10),CONVERT(DATETIME,D2_EMISSAO),103) AS 'DATE',
			D2_CLIENTE   AS 'CCODE',
			A1_NREDUZ   AS 'NAME',
			D2_TIPO   AS 'TYPE',
			D2_SERIE + ' ' + D2_DOC AS 'NUMBER',
			D2_COD   AS 'PCODE',
			B1_DESC   AS 'DESCRIPTION',
			D2_QUANT  AS 'SOLD',
			B2_QATU   AS 'STOCK',
			CONVERT(NUMERIC(10,2),ROUND(D2_CUSTO1,2)) AS 'COST',
			CONVERT(NUMERIC(10,2),	ROUND(D2_TOTAL - D2_VALICM - D2_VALIPI - D2_TOTAL*0.0925,2)) AS 'NETSALE',
			D2_TES  AS 'TES'
		FROM
			Amb01_P10..SD2EF0 A,
			Amb01_P10..SA1EF0 B,
			Amb01_P10..SB1EF0 C,
			Amb01_P10..SB2EF0 D 
		WHERE A.D_E_L_E_T_ <> '*' 
			AND A.D2_CLIENTE = B.A1_COD 
			AND A.D2_CLIENTE <> '000494' 
			AND A.D2_FILIAL = D.B2_FILIAL
			AND (D.B2_FILIAL = %exp:cFil% OR D.B2_FILIAL = '  ')
			AND A.D2_COD = C.B1_COD 
			AND A.D2_COD = D.B2_COD 
			AND A.D2_LOCAL = D.B2_LOCAL 
			AND A.D2_TES NOT in('77T', '80T', '62T', '73A', '94T', '80T', '50T', '58V', '90X', '85T', '55Z', '55V', '84X', '83X', '50Z')
			AND D2_EMISSAO BETWEEN %exp:cDataDe% AND %exp:cDataAte% 
			AND YEAR(CONVERT(DATETIME,D2_EMISSAO))>='2004' 
		ORDER BY
			A.D2_EMISSAO
	EndSql
	
	SQL->(DbGoTop())
	If SQL->(!Bof()) .and. SQL->(!Eof())
		
		// Cabecalho
		cHtml :=	'<table id="tb1">' + CRLF
		cHtml +=	'	<thead>' + CRLF
		cHtml +=	'		<tr>' + CRLF
		cHtml +=	'			<th>C&oacute;digo</th>' + CRLF
		cHtml +=	'			<th>Nome</th>' + CRLF
		cHtml +=	'			<th>Tipo</th>' + CRLF
		cHtml +=	'			<th>C&oacute;digo Produto</th>' + CRLF
		cHtml +=	'			<th>Descri&ccedil;&atilde;o</th>' + CRLF
		cHtml +=	'			<th>Vendidos</th>' + CRLF
		cHtml +=	'			<th>Estoque</th>' + CRLF
		cHtml +=	'			<th>Custo</th>' + CRLF
		cHtml +=	'			<th>Net Sales</th>' + CRLF
		cHtml +=	'			<th>TES</th>' + CRLF
		cHtml +=	'		</tr>' + CRLF
		cHtml +=	'	</thead>' + CRLF
		cHtml +=	'	<tbody>' + CRLF
		
		// Percorre registros do resultado
		While ! SQL->(Eof())
			
			// Define linhas pares e impares
			If lImpar
				cHtml +=	'	<tr class="odd">' + CRLF
				lImpar := .F.
			Else
				cHtml +=	'	<tr class="even">' + CRLF
				lImpar := .T.
			EndIf
			
			// Caso seja uma planilha muda formatacao do codigo
			If lPlanilha
				cHtml +=	'		<td>="'+AllTrim(SQL->CCODE)+'"</td>' + CRLF
			Else
				cHtml +=	'		<td>'+AllTrim(SQL->CCODE)+'</td>' + CRLF
			EndIf
			
			cHtml +=	'		<td>'+AllTrim(SQL->TYPE)+'</td>' + CRLF
			cHtml +=	'		<td>'+AllTrim(SQL->NUMBER)+'</td>' + CRLF
			
			If lPlanilha
				cHtml +=	'		<td>="'+AllTrim(SQL->PCODE)+'"</td>' + CRLF
			Else
				cHtml +=	'		<td>'+AllTrim(SQL->PCODE)+'</td>' + CRLF
			EndIf
			
			cHtml +=	'		<td>'+AllTrim(SQL->DESCRIPTION)+'</td>' + CRLF
			cHtml +=	'		<td>'+AllTrim(Str(SQL->SOLD))+'</td>' + CRLF
			cHtml +=	'		<td>'+AllTrim(Str(SQL->STOCK))+'</td>' + CRLF
			cHtml +=	'		<td>'+AllTrim(Str(SQL->COST))+'</td>' + CRLF
			cHtml +=	'		<td>'+AllTrim(Str(SQL->NETSALE))+'</td>' + CRLF
			cHtml +=	'		<td>'+AllTrim(SQL->TES)+'</td>' + CRLF
			cHtml +=	'	</tr>' + CRLF
			
			SQL->(DbSkip())
		EndDo
		
		// Finaliza a tabela
		cHtml +=	'	</tbody>' + CRLF
		cHtml +=	'</table>'
		
	Else
		cHtml += '<div>N&atilde;o foram encontrados registros</div>'
	EndIf
	
	SQL->(DbCloseArea())
	TcUnlink(nCon)
Return cHtml
