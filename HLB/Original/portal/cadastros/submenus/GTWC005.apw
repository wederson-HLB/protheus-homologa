#include "apwebex.ch"
#include "totvs.ch"
#include 'tbiconn.ch'

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GTWC005 ºAutor  ³Eduardo C. Romanini  º  Data ³  28/05/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Cadastro de submenus do portal.                             º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Grant Thornton                                             º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
*---------------------*
User Function GTWC005()
*---------------------*
Local cHtml	:= "" 

WEB EXTENDED INIT cHtml

If Select("SX2") == 0
	U_WFPrepEnv()
EndIf

cHtml := ExecInPage("GTWC005")

WEB EXTENDED END
	 
Return cHtml

/*
Função  : GTWCM05
Objetivo: Manutenção do cadastro de submenus
Autor   : Eduardo C. Romanini
Data    : 28/05/2012
*/
*---------------------*
User Function GTWCM05()
*---------------------*
Local cHtml	:= "" 
Local cOper := ""

WEB EXTENDED INIT cHtml

If Select("SX2") == 0
	U_WFPrepEnv()
EndIf

cHtml := ExecInPage("GTWCM05")

WEB EXTENDED END
	 
Return cHtml

/*
Função  : WC005Grv()
Objetivo: Gravação dos dados
Autor   : Eduardo C. Romanini
Data    : 28/05/2012
*/
*----------------------*
User Function WC005Grv()
*----------------------*
Local cHtml	:= "" 
Local cOper := ""

Local aPost := ""

WEB EXTENDED INIT cHtml

If Select("SX2") == 0
	U_WFPrepEnv()
EndIf

//Parametro recebido na chamada da função.
cOper := HttpGet->cOper

If Valtype(cOper) <> "U" .and. cOper <> "VIS"

	//Inclusão
	If cOper == "INC"
	    ZW5->(RecLock("ZW5",.T.))
		    
		ZW5->ZW5_CODIGO := GeraCodigo()	
		
	//Alteração
	ElseIf cOper == "ALT"
		ZW5->(RecLock("ZW5",.F.))
	EndIf
		
	SX3->(DbSetOrder(1))
	If SX3->(DbSeek("ZW5"))
		While SX3->(!EOF())
			If  X3Usado(SX3->X3_CAMPO)
				ZW5->&(SX3->X3_CAMPO) := &("HttpPost->"+AllTrim(SX3->X3_CAMPO))
			EndIf
			SX3->(DbSkip())
		EndDo
	EndIf
			
	ZW5->(MsUnlock())
EndIf

cHtml := ExecInPage("GTWC005")

WEB EXTENDED END
	 
Return cHtml  

/*
Função  : GeraCodigo
Objetivo: Gera um novo código para o cadastro de menu 
Autor   : Eduardo C. Romanini
Data    : 28/05/2012
*/
*--------------------------*
Static Function GeraCodigo()
*--------------------------*
Local cRet := ""

Local nCod := 0

If Select("SX2") == 0
	U_WFPrepEnv()
EndIf

BeginSql Alias 'QRY'
	SELECT MAX(ZW5_CODIGO) as 'ULTCOD'
	FROM %table:ZW5%
	WHERE %notDel%
EndSql

QRY->(DbGoTop())
If QRY->(!BOF()) .and. QRY->(!EOF())
    
	//Verifica o ultimo codigo utilizado pela empresa
	nCod := Val(Right(QRY->ULTCOD,5))
	//Soma 1 ao codigo
	nCod++

	//Tranforma o numero para gravação
	cRet := "S"+StrZero(nCod,5)
Else
	//Primeiro numero de solicitação
	nCod := "S00001"
EndIf

QRY->(DbCloseArea())

Return cRet

/*
Função  : WC005Menu
Objetivo: Rotina de busca dos Menus	
Autor   : Eduardo C. Romanini
Data    : 29/05/2012
*/
*-----------------------*
User Function WC005Menu()
*-----------------------*
Local cHtml  := ""
Local cWhere := ""
Local cTexto := HttpGet->term
Local cBanco := ""

WEB EXTENDED INIT cHtml

If Select("SX2") == 0
	U_WFPrepEnv()
EndIf

If Len(cTexto) > 0
    
	cWhere := "% UPPER(ZW3_CODIGO) LIKE '%"+Upper(AllTrim(cTexto))+"%' %"

    BeginSql Alias 'QRY'
		SELECT TOP 5 ZW3_CODIGO, ZW3_TITULO
		FROM %table:ZW3%
		WHERE %notDel%
		  AND %exp:cWhere%
		ORDER BY ZW3_CODIGO
    EndSql

	QRY->(DbGoTop())
	If QRY->(!BOF() .and. !EOF())	
    	
    	cHtml += "["
	EndIf

	//Monta o retorno no formato JSON.
	nI := 1	
	While QRY->(!EOF())

		If nI > 1 
    		cHtml += ","
		EndIf
	
	    cHtml += "{"
		cHtml += '"cod":"'+Alltrim(QRY->ZW3_CODIGO)+'","desc":"'+EncodeUTF8(Alltrim(QRY->ZW3_TITULO))+'"'
		cHtml += "}"

		nI++          
		
		QRY->(DbSkip()) 		
	EndDo	
	
	If Len(cHtml) > 0
    	cHtml += "]"
	EndIf
	
	QRY->(DbCloseArea())
	
EndIf

WEB EXTENDED END

Return cHtml
