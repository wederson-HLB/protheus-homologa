#include "apwebex.ch"
#include "totvs.ch"
#include 'tbiconn.ch'

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GTWC003 ºAutor  ³Eduardo C. Romanini  º  Data ³  28/05/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Cadastro de menus do portal.                                º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Grant Thornton                                             º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
*---------------------*
User Function GTWC003()
*---------------------*
Local cHtml	:= "" 

WEB EXTENDED INIT cHtml

If Select("SX2") == 0
	U_WFPrepEnv()
EndIf

cHtml := ExecInPage("GTWC003")

WEB EXTENDED END
	 
Return cHtml

/*
Função  : GTWCM03
Objetivo: Manutenção do cadastro de menus
Autor   : Eduardo C. Romanini
Data    : 28/05/2012
*/
*---------------------*
User Function GTWCM03()
*---------------------*
Local cHtml	:= "" 
Local cOper := ""

WEB EXTENDED INIT cHtml

If Select("SX2") == 0
	U_WFPrepEnv()
EndIf

cHtml := ExecInPage("GTWCM03")

WEB EXTENDED END
	 
Return cHtml

/*
Função  : WC003Grv()
Objetivo: Gravação dos dados
Autor   : Eduardo C. Romanini
Data    : 28/05/2012
*/
*----------------------*
User Function WC003Grv()
*----------------------*
Local cHtml	:= "" 
Local cOper := ""

Local aPost := ""

WEB EXTENDED INIT cHtml

If Select("SX2") == 0
	U_WFPrepEnv()
EndIf

//Parametro recebido na chamada da função.
cOper := HttpGet->cOper

If Valtype(cOper) <> "U" .and. cOper <> "VIS"

	//Inclusão
	If cOper == "INC"
	    ZW3->(RecLock("ZW3",.T.))
		    
		ZW3->ZW3_CODIGO := GeraCodigo()	
		
	//Alteração
	ElseIf cOper == "ALT"
		ZW3->(DbSetOrder(1))
		If ZW3->(DbSeek(xFilial("ZW3")+HttpPost->ZW3_CODIGO))
			ZW3->(RecLock("ZW3",.F.))
		Else
			ZW3->(RecLock("ZW3",.T.))
		EndIf
	EndIf
		
	SX3->(DbSetOrder(1))
	If SX3->(DbSeek("ZW3"))
		While SX3->(!EOF())
			If  X3Usado(SX3->X3_CAMPO)
				ZW3->&(SX3->X3_CAMPO) := &("HttpPost->"+AllTrim(SX3->X3_CAMPO))
			EndIf
			SX3->(DbSkip())
		EndDo
	EndIf
			
	ZW3->(MsUnlock())
EndIf

cHtml := ExecInPage("GTWC003")

WEB EXTENDED END
	 
Return cHtml  

/*
Função  : GeraCodigo
Objetivo: Gera um novo código para o cadastro de menu 
Autor   : Eduardo C. Romanini
Data    : 28/05/2012
*/
*--------------------------*
Static Function GeraCodigo()
*--------------------------*
Local cRet := ""

Local nCod := 0

If Select("SX2") == 0
	U_WFPrepEnv()
EndIf

BeginSql Alias 'QRY'
	SELECT MAX(ZW3_CODIGO) as 'ULTCOD'
	FROM %table:ZW3%
	WHERE %notDel%
EndSql

QRY->(DbGoTop())
If QRY->(!BOF()) .and. QRY->(!EOF())
    
	//Verifica o ultimo codigo utilizado pela empresa
	nCod := Val(Right(QRY->ULTCOD,5))
	//Soma 1 ao codigo
	nCod++

	//Tranforma o numero para gravação
	cRet := "M"+StrZero(nCod,5)
Else
	//Primeiro numero de solicitação
	nCod := "M00001"
EndIf

QRY->(DbCloseArea())

Return cRet
