#INCLUDE "TOTVS.ch" 
#INCLUDE "FIVEWIN.Ch"
/*
Funcao      : UMATR777
Parametros  : Nenhum
Retorno     : Nenhum
Objetivos   : Alteração no fonte padrão do Pick-List para gerar um pedido por página.
Autor       : Renato Rezende
Data/Hora   : 10/06/2013
*/
/*
Funcao      : UMATR777
Parametros  : Nenhum
Retorno     : Nenhum
Objetivos   : Alteração do leiaute do relatório. Mudança na impressão de retrato para paisagem, campos importantes não estavam sendo impressos.
Autor       : Renato Rezende
Data/Hora   : 23/12/2019
*/
*------------------------*
 User Function UMATR777 ()
*------------------------*

Local wnrel   := "MATR777"
Local tamanho := "G"
Local titulo  := "Pick-List  (Expedicao)" //OemToAnsi(STR0001)
Local cDesc1  := "Emissao de produtos a serem separados pela expedicao, para"// OemToAnsi(STR0002)
Local cDesc2  := "determinada faixa de pedidos." //OemToAnsi(STR0003)
Local cDesc3  := ""
Local cString := "SC9"
Local cPerg   := "MTR777"

PRIVATE aReturn  := {"Zebrado", 1,"Administracao", 2, 2, 1, "",0 }//{STR0004, 1,STR0005, 2, 2, 1, "",0 }
PRIVATE nomeprog := "MATR777"
PRIVATE nLastKey := 0
PRIVATE nBegin   := 0
PRIVATE aLinha   := {}
PRIVATE li       := 80
PRIVATE limite   := 132
PRIVATE lRodape  := .F.
PRIVATE m_pag    := 1 
PRIVATE cPedido  := "" //RRP - 10/06/2013

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica as perguntas selecionadas                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AjustaSX1(cPerg)
pergunte(cPerg,.F.)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                      ³
//³ mv_par01  De Pedido                                       ³
//³ mv_par02  Ate Pedido                                      ³
//³ mv_par03  Imprime pedidos ? 1 - Estoque                   ³
//³                             2 - Credito                   ³
//³                             3 - Estoque/Credito           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
wnrel:=SetPrint(cString,wnrel,cPerg,@Titulo,cDesc1,cDesc2,cDesc3,.F.,,,Tamanho,,.T.)

If nLastKey == 27
	dbClearFilter()
	Return
Endif

SetDefault(aReturn,cString)

If nLastKey == 27
	dbClearFilter()
	Return
Endif

RptStatus({|lEnd| C777Imp(@lEnd,wnRel,cString,cPerg,tamanho,@titulo,@cDesc1,@cDesc2,@cDesc3)},Titulo)
Return
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ C777IMP  ³ Autor ³ Flavio Luiz Vicco     ³ Data ³ 30.06.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Chamada do Relatorio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR777                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
*-------------------------------------------------------------------------------------*
 Static Function C777Imp(lEnd,WnRel,cString,cPerg,tamanho,titulo,cDesc1,cDesc2,cDesc3)
*-------------------------------------------------------------------------------------*

Local cFilterUser := aReturn[7]
Local lUsaLocal  := (SuperGetMV("MV_LOCALIZ") == "S")
Local cbtxt      := SPACE(10)
Local cbcont	 := 0
Local lQuery     := .F.
Local lRet       := .F.
Local cEndereco  := ""
Local nQtde      := 0
Local cAliasNew  := "SC9"
#IFDEF TOP
	Local aStruSC9   := {}
	Local cName      := ""
	Local cQryAd     := ""
	Local nX         := 0
#ELSE
	Local cFilter    := ""
	Local cKey 	     := ""
#ENDIF
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para Impressao do Cabecalho e Rodape    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
li := 80
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Definicao dos cabecalhos                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
titulo := "PICK-LIST" //OemToAnsi(STR0007)
// "Codigo          Desc. do Material                                                                                                        UM Quantidade  Amz Endereco       Lote      SubLote  Dat.de Validade Potencia"
//            1         2         3         4         5         6         7         8         9        10        11        12        13         14        15        16        17        18        19        20        21    
//  01234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890
#IFDEF TOP
	cAliasNew:= GetNextAlias()
	aStruSC9 := SC9->(dbStruct())
	lQuery := .T.
	cQuery := "SELECT SC9.R_E_C_N_O_ SC9REC,"
	cQuery += "SC9.C9_PEDIDO,SC9.C9_FILIAL,SC9.C9_QTDLIB,SC9.C9_PRODUTO, "
	cQuery += "SC9.C9_LOCAL,SC9.C9_LOTECTL,SC9.C9_POTENCI,"
	cQuery += "SC9.C9_NUMLOTE,SC9.C9_DTVALID,SC9.C9_NFISCAL"
	If lUsaLocal
		cQuery += ",SDC.DC_LOCALIZ,SDC.DC_QUANT,SDC.DC_QTDORIG"
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Esta rotina foi escrita para adicionar no select os campos do SC9 usados no filtro do usuario ³
	//³quando houver, a rotina acrecenta somente os campos que forem adicionados ao filtro testando  ³
	//³se os mesmo ja existem no selec ou se forem definidos novamente pelo o usuario no filtro.     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !Empty(aReturn[7])
		For nX := 1 To SC9->(FCount())
			cName := SC9->(FieldName(nX))
			If AllTrim( cName ) $ aReturn[7]
				If aStruSC9[nX,2] <> "M"
					If !cName $ cQuery .And. !cName $ cQryAd
						cQryAd += ",SC9."+ cName
					EndIf
				EndIf
			EndIf
		Next nX
	EndIf 
			
	cQuery += cQryAd
	cQuery += " FROM "
	cQuery += RetSqlName("SC9") + " SC9 "
	If lUsaLocal
		cQuery += "LEFT JOIN "+RetSqlName("SDC") + " SDC "
		cQuery += "ON SDC.DC_PEDIDO=SC9.C9_PEDIDO AND SDC.DC_ITEM=SC9.C9_ITEM AND SDC.DC_SEQ=SC9.C9_SEQUEN AND SDC.D_E_L_E_T_ = ' '"
	EndIf
	cQuery += "WHERE SC9.C9_FILIAL  = '"+xFilial("SC9")+"'"
	cQuery += " AND  SC9.C9_PEDIDO >= '"+mv_par01+"'"
	cQuery += " AND  SC9.C9_PEDIDO <= '"+mv_par02+"'"
	If mv_par03 == 1 .Or. mv_par03 == 3
		cQuery += " AND SC9.C9_BLEST  = '  '"
	EndIf
	If mv_par03 == 2 .Or. mv_par03 == 3
		cQuery += " AND SC9.C9_BLCRED = '  '"
	EndIf
	cQuery += " AND SC9.D_E_L_E_T_ = ' '"
	cQuery += "ORDER BY SC9.C9_FILIAL,SC9.C9_PEDIDO,SC9.C9_CLIENTE,SC9.C9_LOJA,SC9.C9_PRODUTO,SC9.C9_LOTECTL,"
	cQuery += "SC9.C9_NUMLOTE,SC9.C9_DTVALID"
	cQuery := ChangeQuery(cQuery)
	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasNew,.T.,.T.)
	For nX := 1 To Len(aStruSC9)
		If aStruSC9[nX][2] <> "C" .and.  FieldPos(aStruSC9[nX][1]) > 0
			TcSetField(cAliasNew,aStruSC9[nX][1],aStruSC9[nX][2],aStruSC9[nX][3],aStruSC9[nX][4])
		EndIf
	Next nX
#ELSE
	dbSelectArea(cString)
	cIndexSC9 := CriaTrab(nil,.F.)
	cKey :="C9_FILIAL+C9_PEDIDO+C9_CLIENTE+C9_LOJA+C9_PRODUTO+C9_LOTECTL+C9_NUMLOTE+DTOS(C9_DTVALID)"
	cFilter := "C9_FILIAL  = '" + xFilial("SC9") + "' .And. "
	cFilter += "C9_PEDIDO >= '"+mv_par01+"' .And. "
	cFilter += "C9_PEDIDO <= '"+mv_par02+"'"
	If mv_par03 == 1 .Or. mv_par03 == 3
		cFilter += " .And. C9_BLEST  = '  '"
	EndIf
	If mv_par03 == 2 .Or. mv_par03 == 3
		cFilter += " .And. C9_BLCRED = '  '"
	EndIf
	IndRegua(cAliasNew,cIndexSC9,cKey,,cFilter,STR0008) //"Selecionando Registros..."
	dbSetIndex(cIndexSC9+OrdBagExt())
#ENDIF
SetRegua(RecCount())
(cAliasNew)->(dbGoTop())
While (cAliasNew)->(!Eof())

	If!Empty(cFilterUser) .AND. !(&cFilterUser)
		dbSelectArea(cAliasNew)
		dbSkip()
		Loop
	EndIf

#IFDEF TOP
	If lUsaLocal
		cEndereco := (cAliasNew)->DC_LOCALIZ
		nQtde     := (cAliasNew)->DC_QUANT
	Else
		cEndereco := ""
		nQtde     := (cAliasNew)->C9_QTDLIB
	EndIf
	lRet := C777ImpDet(cAliasNew,lQuery,nQtde,cEndereco,@lEnd,titulo,cDesc1,cDesc2,cDesc3,tamanho)
#ELSE
	If lUsaLocal
		cSeek := xFilial("SDC")+(cAliasNew)->(C9_PRODUTO+C9_LOCAL+"SC6"+C9_PEDIDO+C9_ITEM+C9_SEQUEN)
		SDC->(dbGoTop())
		SDC->(dbSetOrder(1))
		SDC->(dbSeek(cSeek))
		While SDC->(!EOF()) .And. cSeek == SDC->(DC_FILIAL+DC_PRODUTO+DC_LOCAL+DC_ORIGEM+DC_PEDIDO+DC_ITEM+DC_SEQ)
			cEndereco := SDC->DC_LOCALIZ
			nQtde     := SDC->DC_QUANT
			If !(lRet := C777ImpDet(cAliasNew,lQuery,nQtde,cEndereco,@lEnd,titulo,cDesc1,cDesc2,cDesc3,tamanho))
				Exit
			EndIf
			SDC->(dbSkip())
		EndDo
	Else
		lRet := C777ImpDet(cAliasNew,lQuery,(cAliasNew)->C9_QTDLIB,"",@lEnd,titulo,cDesc1,cDesc2,cDesc3,tamanho)
	EndIf
#ENDIF
	If !lRet
		Exit
	EndIf
	(cAliasNew)->(dbSkip())
EndDo

If lRodape
	roda(cbcont,cbtxt,"M")
EndIf

If lQuery
	dbSelectArea(cAliasNew)
	dbCloseArea()
	dbSelectArea("SC9")
Else
	RetIndex("SC9")
	Ferase(cIndexSC9+OrdBagExt())
	dbSelectArea("SC9")
	dbClearFilter()
	dbSetOrder(1)
	dbGotop()
EndIf

If aReturn[5] = 1
	Set Printer TO
	dbCommitAll()
	OurSpool(wnrel)
EndIf
MS_FLUSH()
Return NIL

*-----------------------------------------------------------------------------------------------------*
 Static Function C777ImpDet(cAliasNew,lQuery,nQtde,cEndereco,lEnd,titulo,cDesc1,cDesc2,cDesc3,tamanho)  
*-----------------------------------------------------------------------------------------------------*

Local cabec1 := "Codigo          Desc. do Material              "+Space(90)+"UM Quantidade  Amz Endereco           Lote      SubLote  Validade  Potencia Pedido"//OemToAnsi(STR0006)
Local cabec2 := ""
Local cRodape:= "-----------------------------------------------Este pedido continua na próxima página-----------------------------------------------"

Static lFirst := .T.

If lEnd
	@PROW()+1,001 Psay "CANCELADO PELO OPERADOR"//STR0009
	Return .F.
EndIf
If !lQuery
	IncRegua()
EndIf
// Valida se é um pedido diferente para pular de página.
If li > 73 .OR. (cPedido <> (cAliasNew)->C9_PEDIDO .or. cPedido =="") .or. lFirst //li > 55
	cPedido := (cAliasNew)->C9_PEDIDO
	lFirst  := .F.
	cabec(titulo,cabec1,cabec2,nomeprog,tamanho,15)
	lRodape := .T.
EndIf
SB1->(dbSeek(xFilial("SB1")+(cAliasNew)->C9_PRODUTO))
// ----
/*@ li, 00 Psay (cAliasNew)->C9_PRODUTO	Picture "@!"
@ li, 16 Psay Subs(SB1->B1_DESC,1,30)	Picture "@!"
@ li, 47 Psay SB1->B1_UM   				Picture "@!"
@ li, 50 Psay nQtde						Picture "@E 999,999.99"
@ li, 62 Psay (cAliasNew)->C9_LOCAL
@ li, 66 Psay cEndereco
@ li, 81 Psay (cAliasNew)->C9_LOTECTL	Picture "@!"
@ li, 91 Psay (cAliasNew)->C9_NUMLOTE	Picture "@!"
@ li,101 Psay (cAliasNew)->C9_DTVALID	Picture PesqPict("SC9","C9_DTVALID")
@ li,116 PSay (cAliasNew)->C9_POTENCI	Picture PesqPict("SC9","C9_POTENCI")
@ li,123 Psay (cAliasNew)->C9_PEDIDO	Picture "@!"
*/
// Wederson - 23/12/2019.
@ li,000 Psay (cAliasNew)->C9_PRODUTO	Picture "@!"
@ li,016 Psay SB1->B1_DESC          	Picture "@!"
@ li,137 Psay SB1->B1_UM   				Picture "@!"
@ li,140 Psay nQtde						Picture "@E 999,999.99"
@ li,152 Psay (cAliasNew)->C9_LOCAL
@ li,156 Psay cEndereco
@ li,171 Psay (cAliasNew)->C9_LOTECTL	Picture "@!"
@ li,181 Psay (cAliasNew)->C9_NUMLOTE	Picture "@!"
@ li,191 Psay (cAliasNew)->C9_DTVALID	Picture PesqPict("SC9","C9_DTVALID")
@ li,204 PSay (cAliasNew)->C9_POTENCI	Picture PesqPict("SC9","C9_POTENCI")
@ li,211 Psay (cAliasNew)->C9_PEDIDO	Picture "@!"

li++
//Se o pedido contém mais de 73 produtos imprimir a mensagem de continuação no rodapé
If li == 74
	@ 77, 00 Psay cRodape  
EndIf
Return .T.
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³AjustaSX1 ³Autor  ³ Flavio Luiz Vicco     ³ Data ³ 30.06.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ MATR777                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/ 
*---------------------------------*
 Static Function AjustaSX1(cPerg) 
*---------------------------------*

Local aHelpPor01 := {"Informe o numero do pedido inicial a ser ",    "considerado na selecao."}
Local aHelpEng01 := {"Enter the initial order number to be taken in","consideration."}
Local aHelpSpa01 := {"Digite el numero del pedido inicial que debe ","considerarse en la seleccion."}
Local aHelpPor02 := {"Informe o numero do pedido final a ser ",    "considerado na selecao."}
Local aHelpEng02 := {"Enter the final order number to be taken in","consideration."}
Local aHelpSpa02 := {"Digite el numero del pedido final que debe ","considerarse en la seleccion."}
Local aHelpPor03 := {"Seleciona a condicao do pedido de compras a",    "ser impressa."}
Local aHelpEng03 := {"Select the purchase order terms to print.",      ""}
Local aHelpSpa03 := {"Elija la condicion del pedido de compras que se","debe imprimir."}
U_PUTSX1(cPerg,"01","De pedido ?",       "¿De pedido ?",       "From order ?","mv_ch1","C",6,0,0,"G","","","","","mv_par01","","","","",      "","","","","","","","","","","","",aHelpPor01,aHelpEng01,aHelpSpa01)
U_PUTSX1(cPerg,"02","Ate pedido ?",      "¿A pedido ?",        "To order ?",  "mv_ch2","C",6,0,0,"G","","","","","mv_par02","","","","zzzzzz","","","","","","","","","","","","",aHelpPor02,aHelpEng02,aHelpSpa02)
U_PUTSX1(cPerg,"03","Pedidos liberados?","¿Pedidos Aprobados?","orders ?",    "mv_ch3","N",1,0,3,"C","","","","","mv_par03","Estoque","Stock","Inventory","","Credito","Credito","Credit","Credito/Estoque","Credito/Stock","Credit/Invent.","","","","","","",aHelpPor03,aHelpEng03,aHelpSpa03)
Return
