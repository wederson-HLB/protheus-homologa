#Include "FIVEWIN.Ch"
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Descri‡…o ³Relacao das Solicitacoes de Compras                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/         


/*
Funcao      : D1RRSOCO
Parametros  : Nenhum
Retorno     : Nenhum   
Objetivos   : Relacao das Solicitacoes de Compras 
Autor     	: 
Data     	:  
Obs         : 
TDN         : 
Revisão     : Tiago Luiz Mendonça 
Data/Hora   : 15/03/2012
Módulo      : Compras
Cliente     : Tellabs
*/

*-----------------------*
 User Function D1RRSOCO
*-----------------------*

LOCAL wnrel
LOCAL cDesc1   := "Emite uma relacao para controle das solicitacoes cadastradas ,"	//"Emite um relacao para controle das solicitacoes cadastradas ,"
LOCAL cDesc2   := "seus respectivos pedidos e prazos de entrega."	//"seus respectivos pedidos e prazos de entrega."
LOCAL cDesc3   := ""
LOCAL cString  := "SC1"
LOCAL aOrd      := {" Por Solicitacao    "," Por Produto        "}		//" Por Solicitacao    "###" Por Produto        "

Setprvt("strPedido1,strPedido2")
strPedido1 := "NUMERO ITEM COD. PRODUTO              D E S C R I C A O              PROJETO    TP LINHA QUANTIDADE UM CENTRO    EMISSAO  ENTREGA  SOLICITANTE  DT.LIMITE         SALDO   N.PED/ CODIGO LJ RAZAO SOCIAL         DATA DE"
strPedido2 := "            TELLABS                                                  TELLABS             SOLICITADA    CUSTO              DA S.C.               DE COMPRA         DA S.C. N.COT. FORNEC                         EMISSAO"

//NUMERO ITEM COD. PRODUTO              D E S C R I C A O              PROJETO    TP LINHA QUANTIDADE UM CENTRO    EMISSAO  ENTREGA  SOLICITANTE  DT.LIMITE         SALDO   N.PED/ CODIGO LJ RAZAO SOCIAL         DATA DE
//0         1         2         3         4         5         6         7         8         9        10        11        12        13        14        15        16        17        18        19        20         21        22
//01234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789
//            TELLABS                                                  TELLABS             SOLICITADA    CUSTO              DA S.C.               DE COMPRA         DA S.C. N.COT. FORNEC                         EMISSAO

Setprvt("strProduto1,strProduto2")
strProduto1 := "NUMERO ITEM COD. PRODUTO              D E S C R I C A O              PROJETO    TP LINHA QUANTIDADE UM CENTRO    EMISSAO  ENTREGA  SOLICITANTE  DT.LIMITE         SALDO        CODIGO LJ RAZAO SOCIAL                         "
strProduto2 := "            TELLABS                                                  TELLABS             SOLICITADA    CUSTO              DA S.C.               DE COMPRA         DA S.C.      FORNEC                                         "

//NUMERO ITEM COD. PRODUTO              D E S C R I C A O              PROJETO    TP LINHA QUANTIDADE UM CENTRO    EMISSAO  ENTREGA  SOLICITANTE  DT.LIMITE         SALDO        CODIGO LJ RAZAO SOCIAL                         
//0         1         2         3         4         5         6         7         8         9        10        11        12        13        14        15        16        17        18        19        20         21        22
//01234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789
//            TELLABS                                                  TELLABS             SOLICITADA    CUSTO              DA S.C.               DE COMPRA         DA S.C.      FORNEC                                         

PRIVATE Tamanho	:= "G"
PRIVATE Titulo	:= "Relacao de Solicitacoes de Compras"	//"Relacao de Solicitacoes de Compras"
PRIVATE NomeProg:= "D1RRSOCO"
PRIVATE aReturn	:= { "Zebrado", 1,"Administracao", 1, 2, 1, "",1 }		//"Zebrado"###"Administracao"
PRIVATE nLastKey:= 0
PRIVATE cPerg 	:= "MTR100    "
PRIVATE aInd	:= {}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica as perguntas selecionadas                           ³
//³mv_par01             // a partir do numero                    ³
//³mv_par02             // ate o numero                          ³
//³mv_par03 // Todas/em Aberto/Canceladas/nao entregues/Pendentes³
//³mv_par04             // Data emissao inicial                  ³
//³mv_par05             // Data emissao final                    ³
//³mv_par06             // Mostra Pedido Compra p/ cotacoes      ³
//³mv_par07             // Impr. SC's Firmes, Previstas ou Ambas ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AjustaSX1()
Pergunte(cPerg,.F.)

wnrel:=SetPrint(cString,NomeProg,cPerg,@titulo,cDesc1,cDesc2,cDesc3,.F.,aOrd,.F.,Tamanho)

If nLastKey == 27
	Set Filter To
	Return
Endif

SetDefault(aReturn,cString)

If nLastKey = 27
	Set Filter To
	Return
Endif

RptStatus({|lEnd| C100Imp(@lEnd,wnRel,cString)},Titulo)

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ C100IMP  ³ Autor ³ Cristina M. Ogura     ³ Data ³ 09.11.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Chamada do Relatorio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR100                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function C100Imp(lEnd,WnRel,cString)

LOCAL nCntImpr	:= 0
LOCAL nSldEntre	:= 0
LOCAL nSldProd	:= 0
LOCAL nTotSC	:= 0
Local nIndex	:= 0
LOCAL nOrdem	:= aReturn[8]
LOCAL aSoma		:= {}
Local aIndex	:= {}
LOCAL Cabec1,Cabec2,nX,n
Local lQuery    := .F.
Local cQuery    := ""
Local lPedido   := .F.
Local nOrdSC1   := 1 
Local nRegSC1   := 0
Local nQtdSC1   := 0
Local nQtdSC7   := 0
Local cCotSC1   := ""
Local cProSC1   := ""
Local cItPSC1   := "" 
Local cProduto	:= ""
Local cNumSol   := ""

li := 80
m_pag := 1
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicializa os codigos de caracter Comprimido/Normal da impressora ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta os Cabecalhos                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Titulo := "RELACAO DE SOLICITACOES DE COMPRAS"	+Iif(MV_PAR06 == 1, " (Incluindo Pedidos de Compra)", " ")		//"RELACAO DE SOLICITACOES DE COMPRAS"###" (Incluindo Pedidos de Compra)"
If MV_PAR06 == 1
   Cabec1 := strPedido1	//"NUMERO ITEM PRODUTO         D E S C R I C A O              TP GRUP   QUANTIDADE UM   CENTRO  EMISSAO  ENTREGA SOLICITANTE           DT.LIMITE          SALDO N.PED/ CODIGO LJ RAZAO SOCIAL                          DATA DE "
   Cabec2 := strPedido2	//"                                                                     SOLICITADA       CUSTO           DA S.C.                       DE COMPRA        DA S.C. N.COT. FORNEC                                          EMISSAO "
Else
   Cabec1 := strProduto1	//"NUMERO ITEM PRODUTO         D E S C R I C A O              TP GRUP   QUANTIDADE UM   CENTRO  EMISSAO  ENTREGA SOLICITANTE           DT.LIMITE          SALDO        CODIGO LJ RAZAO SOCIAL                                  "
   Cabec2 := strProduto2	//"                                                                     SOLICITADA       CUSTO           DA S.C.                       DE COMPRA        DA S.C.        FORNEC                                                  "
Endif
*****      123456 12 123456789012345 123456789012345678901234567890 99 9999 999999999.99 99 999999999 99/99/99 99/99/99 XXXXXXXXXX            999999 999999 99 9999999999999999999999999999999999999999 99/99/99 99/99/99 999999999.99
*****      0         1         2         3         4         5         6         7         8         9        10        11        12        13        14        15        16        17        18        19       200       210       220
*****      01234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890

If MV_PAR06 == 1
	#IFNDEF TOP
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Abre o SC7 em outra area para criar uma nova IndRegua        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	ChkFile('SC7',.F.,'TMP')
	aADD(aIndex,CriaTrab(NIL,.F.))
	IndRegua("TMP",aIndex[Len(aIndex)],"C7_FILIAL+C7_NUMCOT+C7_PRODUTO")
	dbSelectArea("SC7")
	aADD(aIndex,CriaTrab(NIL,.F.))
	IndRegua("SC7",aIndex[Len(aIndex)],"C7_FILIAL+C7_NUMSC+C7_ITEMSC+C7_PRODUTO")
	nIndex := RetIndex("SC7")
	dbSetIndex(aIndex[2]+OrdbagExt())
	dbSetOrder(nIndex+1)
	#ENDIF
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Se o usuario escolher a opcao que lista as SC's canceladas   ³
//³ pelo sistema ,e' necessario ativar as deletadas.             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If MV_PAR03 == 3
	SET DELE OFF
Endif

dbSelectArea("SC1")
If nOrdem == 1
	dbSetOrder(1)
	dbSeek(xFilial()+MV_PAR01,.T.)
Else
	dbSetOrder(2)
	dbSeek(xFilial())
EndIf
SetRegua(LastRec())

While !Eof() .And. SC1->C1_FILIAL == xFilial() .And.;
	IIf(nOrdem==1,C1_NUM <= MV_PAR02,.T.)
	If nOrdem == 2
		cValAnt		:= xFilial("SC1")+SC1->C1_PRODUTO
		cProduto    := SC1->C1_PRODUTO
		cQuebra		:= 'cValAnt==xFilial("SC1")+SC1->C1_PRODUTO'
	Else
		cValAnt		:= xFilial("SC1")+SC1->C1_NUM
		cNumSol     := SC1->C1_NUM
		cQuebra 	:= 'cValAnt==xFilial("SC1")+SC1->C1_NUM'
	EndIf
	
	While !Eof() .And. &(cQuebra)
		If lEnd
			@PROW()+1,001 PSAY "CANCELADO PELO OPERADOR"		//"CANCELADO PELO OPERADOR"
			Exit
		Endif
		
		IncRegua()
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Filtra as solicitacoes maior que o numero definido           ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If C1_NUM < mv_par01 .Or. C1_NUM > mv_par02
			dbSkip()
			Loop
		Endif
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Filtra as solicitacoes cancelados pelo sistema               ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If (mv_par03 == 3 .And. !Deleted()) .Or. (mv_par03 == 3 .And. Deleted() .And. !("CANCELADO PELO SISTEMA."$C1_OBS))
			dbSkip()
			Loop
		EndIf
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Filtra as solicitacoes em aberto gerada cotacao ou pedido    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If mv_par03 == 2 .And. (C1_QUANT <= C1_QUJE .Or. !Empty(C1_COTACAO))
			dbSkip()
			Loop
		Endif

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Filtra as solicitacoes completamente atendidas               ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If mv_par03 == 5 .And. C1_QUANT == C1_QUJE
			dbSkip()
			Loop
		Endif
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica intervalo de data de emissao                        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If (C1_EMISSAO < mv_par04 .Or. C1_EMISSAO > mv_par05)
			dbSkip()
			Loop
		Endif

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Filtra as solicitacoes nao entregue                          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If mv_par03 == 4
			If !Empty(C1_COTACAO) .And. C1_COTACAO <> "XXXXXX"
				nRegSC1 := SC1->(RecNo())
				nOrdSC1 := SC1->( IndexOrd()) 
                cCotSC1 := SC1->C1_COTACAO
                cProSC1 := SC1->C1_PRODUTO
                cItPSC1 := SC1->C1_ITEMPED 
				nQtdSC1 := 0
                nQtdSC7 := 0
                lPedido := .F. 
				dbSelectArea("SC7")
				dbSetOrder(2)
				dbSeek(xFilial("SC7")+SC1->C1_PRODUTO)
				While !Eof() .And. xFilial("SC7")+cProSC1 == C7_FILIAL+C7_PRODUTO
					If SC7->C7_NUMCOT == cCotSC1 .And. SC7->C7_ITEM == cItPSC1             
						lPedido := .T.	 
						nQtdSC7 += SC7->C7_QUJE

						dbSelectArea("SC8")
						dbSetOrder(3)
						MsSeek(xFilial("SC8")+SC7->C7_NUMCOT+SC7->C7_PRODUTO+SC7->C7_FORNECE+SC7->C7_LOJA+SC7->C7_NUM+SC7->C7_ITEM)
						dbSelectArea("SC1")
						dbSetOrder(5)
						MsSeek(xFilial("SC1")+SC8->C8_NUM+SC8->C8_PRODUTO+SC8->C8_IDENT)						
						While ( !Eof() .And. xFilial("SC1") == SC1->C1_FILIAL .And. SC8->C8_NUM == SC1->C1_COTACAO .And.;
								          SC8->C8_PRODUTO == SC1->C1_PRODUTO .And. SC8->C8_IDENT == SC1->C1_IDENT .And. ;
								          SC8->C8_NUMPED == SC1->C1_PEDIDO .And. SC8->C8_ITEMPED == SC1->C1_ITEMPED )
							nQtdSC1 += SC1->C1_QUANT
							dbSelectArea("SC1")
							dbSkip()
	                    EndDo 
					Endif
					dbSelectArea("SC7")
					dbSkip()
				EndDo

				dbSelectArea("SC1")
				dbSetOrder(nOrdSC1)
				MsGoto(nRegSC1)

                If lPedido	 	
					nSldEntre := nQtdSC1 - nQtdSC7				
                Else 
					nSldEntre := SC1->C1_QUANT				
                EndIf 

			Else
				nSldEntre := SC1->C1_QUANT
				dbSelectArea("SC7")
				dbSetOrder(2)
				dbSeek(xFilial("SC7")+SC1->C1_PRODUTO)
				While !Eof() .And. xFilial( "SC7" ) +SC1->C1_PRODUTO == C7_FILIAL+C7_PRODUTO
					If C7_NUMSC == SC1->C1_NUM .And. C7_ITEMSC == SC1->C1_ITEM
						nSldEntre -= C7_QUJE
					Endif
					dbSelectArea("SC7")
					dbSkip()
				EndDo
			Endif
			
			If nSldEntre <= 0
				dbSelectArea("SC1")
				dbSkip()
				Loop
			Endif

		Endif

		dbSelectArea("SC1")
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Filtra Tipo de SCs Firmes ou Previstas                       ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !MtrAValOP(mv_par07, 'SC1')
			dbSkip()
			Loop
		EndIf
		
		If li > 55
			Cabec(Titulo,Cabec1,Cabec2,NomeProg,Tamanho,IIF(aReturn[4]==1,15,18))
		Endif
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Adiciona 1 ao contador de registros impressos         ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		nCntImpr++
		
		@ li,000 PSAY C1_NUM
		@ li,007 PSAY C1_ITEM
		@ li,012 PSAY C1_P_COD
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Imprime descricao completa da SC                             ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		@ li,038 PSAY SubStr(C1_DESCRI,1,30)
		For nX := 31 To Len(Trim(C1_DESCRI)) Step 30
			li++
			@ li,038 PSAY SubStr(C1_DESCRI,nX,30)
		Next nX		
		@ li,069 PSAY C1_P_PRJ
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Posiciona no arquivo de produtos                             ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SB1")
		dbSetOrder(1)
		dbSeek(xFilial()+SC1->C1_PRODUTO)
		
		@ li,080 PSAY B1_TIPO
		@ li,083 PSAY B1_P_LINHA
		
		dbSelectArea("SC1")
		@ li,089 PSAY C1_QUANT          Picture PesqPictQt("C1_QUANT",12)
		@ li,100 PSAY C1_UM
		@ li,103 PSAY PadL(Alltrim(C1_CC),9)    Picture "@!"
		@ li,113 PSAY C1_EMISSAO        Picture PesqPict("SC1","C1_EMISSAO")
		@ li,122 PSAY C1_DATPRF         Picture PesqPict("SC1","C1_DATPRF")
		@ li,131 PSAY Substr(C1_SOLICIT,1,12)
		@ li,144 PSAY SC1->C1_DATPRF - CalcPrazo(SC1->C1_PRODUTO,SC1->C1_QUANT)
		@ li,162 PSAY SC1->C1_QUANT-SC1->C1_QUJE  Picture PesqPictQt("C1_QUANT",12)
		
		nSldProd += ( C1_QUANT - C1_QUJE )
		nTotSc   += C1_QUANT
		
		If MV_PAR06 == 1
			If Empty(C1_COTACAO) .Or. C1_COTACAO == "XXXXXX"
				#IFNDEF TOP
					cAliasSC7 := "SC7"
				dbSelectArea("SC7")
				If dbSeek(xFilial()+SC1->C1_NUM+SC1->C1_ITEM+SC1->C1_PRODUTO)
					While !Eof() .and. C7_FILIAL+C7_NUMSC+C7_ITEMSC == xFilial()+SC1->C1_NUM+SC1->C1_ITEM
				#ELSE
					cAliasSC7 := "TMP"
					lQuery := .T.
					cQuery := "SELECT C7_FILIAL,C7_NUM,C7_FORNECE,C7_LOJA,C7_EMISSAO,C7_TPOP,C7_NUMSC,C7_ITEMSC "
					cQuery += "FROM "+RetSqlName("SC7")+" "					
					cQuery += "WHERE "
					cQuery += "C7_FILIAL = '"+xFilial("SC7")+"' AND "
					cQuery += "C7_NUMSC  = '"+SC1->C1_NUM+"' AND "
					cQuery += "C7_ITEMSC = '"+SC1->C1_ITEM+"' AND "
					cQuery += "D_E_L_E_T_= ' ' "
					cQuery += "ORDER BY C7_FILIAL,C7_NUMSC,C7_ITEMSC"
					cQuery := ChangeQuery(cQuery)
					dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),"TMP",.T.,.T.)
					TcSetField("TMP","C7_EMISSAO","D",8,0)
					If ( Eof() )
						li++
					EndIf
					While !Eof()
				#ENDIF
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Filtra Tipo de SCs Firmes ou Previstas                       ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						If !MtrAValOP(mv_par07, "SC7" )
							dbSkip()
							Loop
						EndIf
						
						@ li,170 PSAY C7_NUM
						@ li,177 PSAY C7_FORNECE
						@ li,184 PSAY C7_LOJA
						dbSelectArea("SA2")
						dbSeek(xFilial()+(cAliasSC7)->C7_FORNECE+(cAliasSC7)->C7_LOJA)
						@ li,187 PSAY SubStr(A2_NOME,1,28)
						@ li,208 PSAY (cAliasSC7)->C7_EMISSAO
						li++
						dbSelectArea(cAliasSC7)
						dbSkip()
					EndDo
					If ( lQuery )
						(cAliasSC7)->(dbCloseArea())
					EndIf
					dbSelectArea("SC7")
					#IFNDEF TOP
				Else
					li++
				EndIf
					#ENDIF
			Else
				#IFNDEF TOP
				dbSelectArea("TMP")
				If dbSeek(xFilial('SC7')+SC1->C1_COTACAO+SC1->C1_PRODUTO)
					@ li,163 PSAY OemToAnsi("Pedidos gerados por cotacao Num.") + SC1->C1_COTACAO // "Pedidos gerados por cotacao - Num. "
					li++
					While !Eof() .And. C7_FILIAL+C7_NUMCOT+C7_PRODUTO  == xFilial('SC7')+SC1->C1_COTACAO+SC1->C1_PRODUTO
				#ELSE
					lQuery := .T.
					cQuery := "SELECT C7_FILIAL,C7_NUM,C7_FORNECE,C7_LOJA,C7_EMISSAO,C7_TPOP,C7_NUMCOT,C7_PRODUTO "
					cQuery += "FROM "+RetSqlName("SC7")+" "
					cQuery += "WHERE "
					cQuery += "C7_FILIAL = '"+xFilial("SC7")+"' AND "
					cQuery += "C7_NUMCOT = '"+SC1->C1_COTACAO+"' AND "
					cQuery += "C7_PRODUTO = '"+SC1->C1_PRODUTO+"' AND "
					cQuery += "D_E_L_E_T_= ' ' "
					cQuery += "ORDER BY C7_FILIAL,C7_NUMCOT,C7_PRODUTO "
					cQuery := ChangeQuery(cQuery)
					dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),"TMP",.T.,.T.)
					TcSetField("TMP","C7_EMISSAO","D",8,0)
					If ( Eof() )
						li++
					Else
						@ li,175 PSAY OemToAnsi("Pedidos gerados por cotacao Num.") + SC1->C1_COTACAO // "Pedidos gerados por cotacao - Num. "
						li++
					EndIf
					While !Eof()
				#ENDIF
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Filtra Tipo de SCs Firmes ou Previstas                       ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						If !MtrAValOP(mv_par07, 'SC7')
							dbSkip()
							Loop
						EndIf
						@ li,170 PSAY C7_NUM
						@ li,177 PSAY C7_FORNECE
						@ li,184 PSAY C7_LOJA
						dbSelectArea("SA2")
						dbSeek(xFilial()+TMP->C7_FORNECE+TMP->C7_LOJA)
						@ li,187 PSAY SubStr(A2_NOME,1,20)
						@ li,208 PSAY TMP->C7_EMISSAO
						li++
						dbSelectArea("TMP")
						dbSkip()
					EndDo
				#IFDEF TOP
					dbSelectArea("TMP")
					dbCloseArea()
					dbSelectArea("SC7")
				#ELSE
				Else
					li++
				EndIf
				#ENDIF
			EndIf
		Else
		    dbSelectArea("SA2")
		    If dbSeek(xFilial()+SC1->C1_FORNECE+SC1->C1_LOJA)
		       dbSelectArea("SC1")
		       @ li,175 PSAY SubStr(SC1->C1_FORNECE,1,6)
		       @ li,182 PSAY SubStr(SC1->C1_LOJA,1,2)
		       @ li,185 PSAY SubStr(SA2->A2_NOME,1,20)
		    Endif   
			li++
		EndIf
		dbSelectArea("SC1")
		dbSkip()
	EndDo
	If lEnd
		Exit
	Endif
	If nOrdem == 2 .And. nTotSC > 0
		li++
		@li, 00 PSay "Total  -  " + Rtrim(cProduto) + " ----> "      //"Total  -  "
		@li, 89 PSay nTotSC   Picture PesqPictQt("C1_QUANT",16)
		@li,162 PSay nSldProd Picture PesqPictQt("C1_QUANT",16)
		li++
		@li, 00 PSay __PrtThinLine()
		nTotSc   := 0
		nSldProd := 0
		li++
	ElseIf nOrdem == 1 .And. nTotSC > 0
		li++
		@li, 00 PSay "Total  -  " + Rtrim(cNumSol) + " ----> "      //"Total  -  "
		@li, 89 PSay nTotSC   Picture PesqPictQt("C1_QUANT",16)
		@li,162 PSay nSldProd Picture PesqPictQt("C1_QUANT",16)
		li++
		@li, 00 PSay __PrtThinLine()
		nTotSc   := 0
		nSldProd := 0
		li++
	EndIf
EndDo

If MV_PAR03 == 3
	SET DELE ON
EndIf

If li != 80
	Roda(nCntImpr,"REGISTRO(S)",Tamanho)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Deleta Arquivo Temporario e Restaura os Indices Nativos.     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SC7")
RetIndex("SC7")
dbSelectArea("SC1")
Set Filter To
RetIndex("SC1")
dbSetOrder(1)
If ( Select("TMP")<>0 )
	dbSelectArea("TMP")
	dbCloseArea()
	dbSelectArea("SC7")
EndIf
#IFNDEF TOP
	For nI:=1 to Len(aIndex)
		If File(aIndex[nI]+OrdBagExt())
			Ferase(aIndex[nI]+OrdBagExt())
		Endif
	Next
#ENDIF

Set Device to Screen

If aReturn[5] = 1
	Set Printer To
	dbCommitAll()
	OurSpool(wnrel)
Endif

MS_FLUSH()

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ AjustaSX1³ Autor ³ Alexandre Inacio Lemes³ Data ³26/05/2004³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Altera descricao da pergunta no SX1                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR100			                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function AjustaSX1()

Local aArea := { Alias(), IndexOrd() } , aPerg:={}
Local cPerg := "MTR100"

Aadd(aPerg,{"Pendentes em PC","Pendiente en PC","Pending in Purc"})

dbSelectArea("SX1")
dbSetOrder(1)
If dbSeek(cPerg+"03")
	RecLock("SX1",.F.)
	Replace X1_DEF05 	with aPerg[1][1]
	Replace X1_DEFSPA5	with aPerg[1][2]
	Replace X1_DEFENG5 	with aPerg[1][3]
	MsUnLock()
EndIf

dbSelectArea( aArea[1] )
dbSetOrder( aArea[2] )
Return
